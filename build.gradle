buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
	}
}

apply plugin: 'java'
apply plugin: 'eu.appsatori.fatjar'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
	jcenter()
}

dependencies{
	//slack api
	compile group: 'com.ullink.slack', name: 'simpleslackapi', version: '0.5.1'

	//general
	compile group: 'commons-io', name: 'commons-io', version: '2.5'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'

	//wordnet
	compile group: 'de.sciss', name: 'ws4j', version: '0.1.0'
	//TO USE WORDNET (for the AI), please download wnjpn.db and add it to src/main/resources (http://nlpwww.nict.go.jp/wn-ja/data/1.1/wnjpn.db.gz)
	

	//database
	compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1209'
	compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.1.Final'

	//trueskill algorithm
	compile files('libs/jskills-0.9.1-SNAPSHOT.jar')//BUILT FROM: https://github.com/nsp/JSkills
	compile group: 'org.ejml', name: 'simple', version:'0.28'
	
	//testing
	testCompile group: 'junit', name: 'junit', version: '4.12'
}


task rankingTest(type:JavaExec){
	main = 'com.raether.watchwordbot.ranking.RankingTest'
	classpath = sourceSets.main.runtimeClasspath
}

task semanticTest(type:JavaExec){
	main = 'com.raether.watchwordbot.meatsim.SemanticSimilarityTest'
	classpath = sourceSets.main.runtimeClasspath
}

task runner(type:JavaExec) {
	main = 'com.raether.watchwordbot.Runner'
	classpath = sourceSets.main.runtimeClasspath
	args = {
		if(project.hasProperty("apiKey")){
			return [getProperty("apiKey")];
		}
		return [];
	}();
}

jar {
	// Keep jar clean:
	exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

	manifest { attributes 'Main-Class': 'com.raether.watchwordbot.Runner' }
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
}